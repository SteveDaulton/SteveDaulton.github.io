<title>additive synthesis - b2.lsp</title>

<p align=center><b>
<a href="https://forum.audacityteam.org/">Audacity Support Forum</a> /
<a href="../../nyquist.html">Audacity and Nyquist</a> /
<a href="../../manual/home.html">Nyquist Reference Manual</a> /
<a href="../examples.html">Nyquist Examples and Tutorials</a>
</b></p>

<p align=center>Back to <a href="main.html">Pedro Morales' Examples Page</a></p>

<h1 align=center>Risset's Spectral Analysis of a Chord</h1>

<p align=center>Sound Example: <a href="b2-spectral.ogg">spectral.ogg</a></p>

<br>

<pre>
;;; ADDITIVE SYNTHESIS
;;; Risset's Spectral Analysis of a Chord
;;; coded by Pedro Jose Morales
;;; pmorales@iele-ab.uclm.es

(load "pjmg.lsp")

; Probar con las dos envolventes
;(defun sac-env (dur)
;  (let ((xx (pwl (/ dur 2) 1.0 dur)))
;    (mult xx xx)))

(defun sac-env (dur)
  (pwev 1.0 dur 5e-2))

(defun attk-list (offset num-harms &optional (out-list (list 0.0)))
  (if (= num-harms 0) (reverse out-list)
      (attk-list offset (1- num-harms) (cons (+ offset (car out-list)) out-list))))

  
(defun sac (frq dur offset-entry num-harm)
    (mapcar #'(lambda (xhrm xoff)
                (at xoff (amosc (hz-to-step (* (step-to-hz frq) xhrm)) (sac-env dur))))
           (attk-list -1 (1- num-harm) (list num-harm))
           (attk-list offset-entry (1- num-harm))))

(defun sac-left-right (l)
  (do* ((i 0 (1+ i))
        (left () (if (evenp i) (cons (nth i l) left) left))
        (right () (if (oddp i) (cons (nth i l) right) right)))
       ((= i  (1- (length l))) (vector (apply #'sim left) (apply #'sim right))))) 

(defun st-sac (frq dur offset-entry num-harm)
  (sac-left-right (sac frq dur offset-entry (1+ num-harm))))

(autonorm-off)
(play (scale 0.17 (sim (at 0.0 (st-sac as6 7.5 2.5 5))
                        (at 0.01 (st-sac b5 7.5 2.5 5))
                        (at 3.75 (st-sac e5 3.75 1.25 9))
                        (at 3.76 (st-sac g5 3.75 1.25 9))
                        (at 5.5 (st-sac d4 2 1.0 11))
                        (at 5.51 (st-sac gs3 2 1.0 11)))) )
</pre>

<br>

<p align=center>Back to <a href="main.html">Pedro Morales' Examples Page</a></p>

<p align=center><b>
<a href="https://forum.audacityteam.org/">Audacity Support Forum</a> /
<a href="../../nyquist.html">Audacity and Nyquist</a> /
<a href="../../manual/home.html">Nyquist Reference Manual</a> /
<a href="../examples.html">Nyquist Examples and Tutorials</a>
</b></p>
